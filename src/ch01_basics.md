# Глава 1. Основы интерпретации

В этой главе описывается базовый интерпретатор, идеи которого проходят красной нитью через большую часть этой книги.
Он намеренно сделан простым и более близким к Scheme, чем к Лиспу, фактически описывая Лисп в терминах Scheme.
В этой вводной главе мы рассмотрим следующие темы: суть интерпретации;
взаимосвязь функций `eval` и `apply`; свойства окружений и функций.
Короче говоря, мы начнём рассматривать здесь то, что будем изучать подробнее в следующих главах.
Пусть вас не отпугнёт пропасть незнания по обе стороны моста, которым мы пойдём.

Интерпретатор и его варианты будут написаны на Scheme без использования каких‑либо существенных особенностей данного диалекта.

В книгах по Лиспу редко когда отказываются от нарциссического соблазна описать Лисп с помощью Лиспа.
Начало традиции положило первое руководство по LISP 1.5 [[MAE<sup>+</sup>62]](zz_bibliography.html#mae+62),
и впоследствии такой подход широко распространился.
Вот лишь малая часть существующих примеров:
[[Rib69]](zz_bibliography.html#rib69),
[[Gre77]](zz_bibliography.html#gre77),
[[Que82]](zz_bibliography.html#que82),
[[Cay83]](zz_bibliography.html#cay83),
[[Cha80]](zz_bibliography.html#cha80),
[[SJ93]](zz_bibliography.html#sj93),
[[Rey72]](zz_bibliography.html#rey72),
[[Gor75]](zz_bibliography.html#gor75),
[[SS75]](zz_bibliography.html#ss75),
[[All78]](zz_bibliography.html#all78),
[[McC78b]](zz_bibliography.html#mcc78b),
[[Lak80]](zz_bibliography.html#lak80),
[[Hen80]](zz_bibliography.html#hen80),
[[BM82]](zz_bibliography.html#bm82),
[[Cli84]](zz_bibliography.html#cli84),
[[FW84]](zz_bibliography.html#fw84),
[[dRS84]](zz_bibliography.html#drs84),
[[AS85]](zz_bibliography.html#as85),
[[R3R86]](zz_bibliography.html#r3r86),
[[Mas86]](zz_bibliography.html#mas86),
[[Dyb87]](zz_bibliography.html#dyb87),
[[WH89]](zz_bibliography.html#wh89),
[[Kes88]](zz_bibliography.html#kes88),
[[LF88]](zz_bibliography.html#lf88),
[[Dil88]](zz_bibliography.html#dil88),
[[Kam90]](zz_bibliography.html#kam90).

Эти интерпретаторы довольно сильно разнятся: как языками, которые они реализуют и используют для реализации,
так и — что более важно — целями, которые они преследуют.
Например, интерпретатор из [[Lak80]](zz_bibliography.html#lak80) показывает,
как объекты и концепции компьютерной графики естественным образом реализуются на Лиспе;
в то время как интерпретатор, описываемый в [[BM82]](zz_bibliography.html#bm82),
создан для замера сложности интерпретируемых программ.

<a id="indexR:язык!реализации"></a>
<a id="indexR:язык!реализуемый"></a>
Язык, *используемый* для реализации, тоже играет немалую роль.
Если в нём есть присваивание и доступ к памяти (`set-car!` и `set-cdr!`),
это даёт бо́льшую свободу и делает исходный код интерпретатора более компактным.
Мы получаем возможность описать язык в терминах, которые близки к машинным инструкциям.
Конечно, это усложняет составление описания, но в то же время уменьшает риск семантических расхождений.
Даже если подобное описание занимает больше места, чем высокоуровневое,
оно даёт более точное понимание смысла происходящего при интерпретации — для нас важно именно это.

\input{figures/fig1.1}
<p class="caption">Уровни сложности.</p>

На рисунке показано сравнение уровней сложности определяющего (по оси *x*)
и определяемого (по оси *y*) языков для некоторых из интерпретаторов.
Здесь хорошо виден ход развития наших знаний со временем:
всё более сложные задачи решаются с использованием всё более ограниченных возможностей.
Эта книга соответствует вектору, который начинается использованием
высокоуровневого Лиспа для реализации Scheme, а заканчивается реализацией
высокоуровневого Лиспа с помощью одного лишь λ-исчисления.

## Вычисления

<a id="indexC:eval"></a>
<a id="indexR:вычислитель"></a>
Важнейшая часть интерпретатора Лиспа содержится в одной функции, вокруг которой крутится все остальное.
Эта функция, называемая `eval`, принимает на вход программу, а на выходе даёт результат её исполнения.
Явное наличие исполнителя кода отнюдь не случайно, а наоборот, является характерной и намеренно реализованной чертой Лиспа.

<a id="indexR:язык!универсальный"></a>
<a id="indexR:универсальный язык"></a>
<a id="indexR:машина Тьюринга"></a><a id="indexR:Тьюринга, машина"></a>
Язык программирования называется _универсальным_, если он не уступает в выразительных возможностях машине Тьюринга.
Так как машина Тьюринга довольно примитивна, то несложно разработать язык, который бы удовлетворял этому условию.
Действительно, наверное сложнее будет придумать полезный язык, который бы при этом не оказался полным по Тьюрингу.

<a id="indexR:тезис Чёрча"></a><a id="indexR:Чёрча, тезис"></a>
В соответствии с тезисом Чёрча, любая вычислимая функция может быть записана на любом тьюринг-полном языке.
Интерпретатор Лиспа можно представить как такую функцию, которая принимает программы и возвращает результаты их исполнения.
Так что если такая функция вообще существует, её можно записать на любом тьюринг-полном языке.
Следовательно, функцию-вычислитель Лиспа `eval` можно записать в частности на том же самом Лиспе.
В этом нет никаких противоречий: точно так же, к примеру, можно реализовать Фортран на Фортране.

Но что делает Лисп уникальным (и делает `eval` интересной), так это небольшой размер интерпретатора:
обычно от одной до двадцати страниц кода, в зависимости от детализации.[^1]
Таков результат желания сделать язык последовательным, с минимальным количеством исключений из правил,
и, что самое главное, с простым, но выразительным синтаксисом.

[^1]: Интерпретатор, описываемый в этой главе, занимает около 150 строк.

<a id="indexC:eval!свойства"></a>
Сам факт существования `eval`, а также возможность её описания на Лиспе имеют несколько интересных следствий.

  - Можно изучить Лисп или прочитав руководство по языку (в котором описываются все доступные возможности),
    или непосредственно изучив функцию `eval`.
    Второй подход сложен тем, что надо уже знать Лисп для того, чтобы понять описание `eval` —
    но ведь знание Лиспа по идее должно быть *следствием* изучения `eval`, нежели *предпосылкой* для него.
    На самом деле, достаточно знать лишь ту часть Лиспа, которая используется для описания `eval`.
    Кроме того, язык, определяемый одной `eval`, не является всем Лиспом:
    он есть лишь сутью языка, где реализованы только специальные формы и немного примитивных функций.

    Тем не менее, несомненно преимущество Лиспа в наличии возможности изучать язык двумя разными, но всё же связанными путями.

  - Тот факт, что `eval` написана на Лиспе, значит также и то, что
    среда разработки является составной частью языка и не требует значительных накладных расходов.
    Под средой разработки понимаются такие вещи как отладчик, трассировщик
    или даже возможность обратного хода [[Lie87]](zz_bibliography.html#lie87).
    Практически, реализация таких инструментов — это лишь доработка `eval`,
    к примеру, чтобы она выводила сообщения при вызове функций,
    приостанавливала вычисления в интересующих местах и так далее.

    Долгое время среда разработки с такими возможностями была уникальной для Лиспа.
    Но и сегодня то, что `eval` может быть описана на самом Лиспе, даёт возможность легко экспериментировать с новыми вариантами реализации вычислений или отладки.

  - Наконец, сама по себе `eval` способна быть инструментом программирования.
    Достаточно спорным инструментом, так как использование `eval` требует присутствия в памяти целого интерпретатора или компилятора во время исполнения кода.
    Кроме того, в таком случае становится невозможным применять некоторые оптимизации.
    Другими словами, использование `eval` имеет свою цену.
    В некоторых случаях её использование полностью оправдано,
    к примеру, когда Лисп используется для описания и реализации метаязыков.

    Кроме ощутимой стоимости использования, семантика `eval` часто неоднозначна.
    Именно поэтому она вообще не входила в стандарт Scheme до ревизии R<sup>5</sup>RS
    [[CR91b]](zz_bibliography.html#cr91b), [[KCR98]](zz_bibliography.html#kcr98).
    Мы рассмотрим этот вопрос подробнее [в восьмой главе](ch08_evaluation_and_reflection.html).

## Базовый вычислитель

<a id="indexR:переменные!свободные"></a><a id="indexR:переменные!связанные"></a>
<a id="indexR:свободные переменные"></a>
<a id="indexR:связывающие формы"></a><a id="indexR:форма!связывающая"></a>
<a id="indexR:окружение"></a>
<a id="indexC:evaluate"></a>
Будем различать в программе _свободные_ и _связанные переменные_.
Переменная свободна, если ни одна связывающая форма (вроде `let` или `lambda`) не связывает её с каким-либо значением.
В противном случае она называется связанной.
Соответственно, свободные переменные могут свободно принимать любое значение,
и нельзя определённо сказать, какое именно, не выходя за рамки функции.
Структура данных, которая связывает переменные с их значениями, называется _окружением_.
Таким образом, функция `evaluate`[^2] имеет два аргумента:
программу и окружение, в котором она должна быть исполнена:

```scheme
(define (evaluate exp env) ...)
```

[^2]: Как вы знаете, «родная» функция-вычислитель Scheme называется `eval`.
      Чтобы их не путать, нашу функцию мы назовём `evaluate`.
      По тем же причинам местный аналог `apply` будет называться `invoke`.
      Меньше перекрытий имён — меньше проблем.

## Вычисляем атомы

Одна из ключевых особенностей Лиспа состоит в том, что программы записываются теми же конструкциями, что и остальные данные.
Но так как любая запись подразумевает определённые условности, поговорим о таких условностях для записи программ.
Главные соглашения: переменные записываются символами (своими именами),
а вызовы функций — списками, где первый элемент — это вызываемая функция, а остальные — её аргументы.

<a id="indexR:атом"></a>
Как и любой другой компилятор, `evaluate` начинает свою работу с синтаксического анализа переданного ей выражения, чтобы выяснить, что именно оно означает.
В этом смысле, название раздела не совсем правильное:
мы рассматриваем не буквально вычисление атомов, а интерпретацию программ, состоящих из атомов.
Сейчас важно отличать саму программу от её представления (письмо от листа бумаги, на котором оно написано).
Функция `evaluate` работает с представлением программ,
по нему она узнаёт предписываемые программой действия и выполняет их.

<a id="indexC:atom?"></a>
```scheme
(define (evaluate exp env)
  (if (atom? exp)               ; (atom? exp) ≡ (not (pair? exp))
      ...
      (case (car exp)
        ...
        (else ...) ) ) )
```

Если выражение не является списком, то оно может быть символом, или числом, или строкой.
Если это всё же символ, то он представляет _переменную_, а её значение хранится в окружении:

```scheme
(define (evaluate exp env)
  (if (atom? exp)
      (if (symbol? exp) (lookup exp env) exp)
      (case (car exp)
        ...
        (else ...) ) ) )
```

Функция `lookup` (которая рассматривается подробнее [в своём разделе](#Представление-окружений))
знает, как отыскать значение переменной в окружении.
Вот её прототип:

<a id="indexC:lookup"></a>
```scheme
(lookup переменная окружение) → значение
```

<a id="indexR:переменные!связь с символами"></a>
<a id="indexR:символы!связь с переменными"></a>
Как видим, здесь происходит неявное преобразование символа в переменную.
Если быть более дотошным, тогда вместо `(lookup exp env)` надо записать что‑то вроде:

```scheme
... (lookup (symbol->variable exp) env) ...
```

<a id="indexR:приведение типов"></a>
<a id="indexR:объекты!второго класса"></a>
В таком случае мы явно говорим, что значение переменной `exp`,
то есть символ с именем переменной должен быть превращён в переменную.
Также это подчёркивает тот факт, что функция `symbol->variable`[^3] вовсе не переводит значение `exp` само в себя;
она превращает синтаксическую сущность (символ) в семантическую (переменную).
В действительности, переменные — это лишь воображаемые объекты,
которым язык и программист дали какие‑то имена, и которые ради удобства используются в форме имён.
Способ представления имён также выбран из соображений удобства, так как Лисп имеет базовый тип символов.
В данном случае `symbol->variable` ничего не делает,
хотя в других языках может применяться какая-нибудь другая форма записи имени переменной.
Например: если это строка, состоящая из знака доллара и имени переменной,
то конечно же `symbol->variable` будет уже не такой тривиальной.

[^3]: Лично я не люблю называть функции приведения типов `x‑>y`,
      потому что так сложнее понимать цепочки преобразований.
      Запись `(y‑>z(x‑>y ...))` не так очевидна, как `(z<‑y(y<‑x ...))`.
      Хотя, с другой стороны, одиночная запись `z<‑y` не так легко читается, как `y‑>z`.
      Приходится выбирать.

Если бы переменные действительно были лишь воображаемыми,
то `lookup` не знала бы как обрабатывать свой первый аргумент,
ведь она ожидает нечто «материальное».
Так что нам надо преобразовать переменную в её программное представление,
какой-нибудь уникальный ключ, по которому `lookup` сможет отыскать переменную в окружении.
Поэтому ещё точнее было бы записать:

```scheme
... (lookup (variable->key (symbol->variable exp)) env) ...
```

Однако, врождённая лень лисперов настаивает на использовании символов для ключей.
Так что `variable‑>key` — это лишь обратная функция к `symbol->variable`,
и их последовательное применение никак не изменяет `exp`.

<a id="indexR:автоцитирование"></a>
<a id="indexR:механизм автоцитирования"></a>
Если выражение атомарное (то есть не является списком) и не является символом,
то его обычно считают представлением какой-нибудь константы с соответствующим значением.
Такое поведение называется механизмом _автоцитирования_.
Автоцитируемый объект не требует явного цитирования и имеет собственное значение.
За примерами можно обратиться к [[Cha96]](zz_bibliography.html#cha96).

Но является ли такое поведение правильным?
Во-первых, не всегда атомарные объекты обозначают сами себя.
Например, строка `"a?b:c"` могла бы означать вызов компилятора Си,
затем вызов получившейся программы и подстановку возвращаемого ей значения вместо этой строки.

С другими объектами (вроде функций) вообще не понятно, как именно их *вычислять*.
К примеру, ясно, что значением переменной `car` является функция, возвращающая левый элемент пары.
Мы можем её вызвать, но что является значением самой *функции car*?
Чаще всего попытки вычисления значения функции являются ошибками в программе.

<a id="indexC:()"></a><a id="indexR:пустой список, ()"></a>
Другой пример проблемного значения — пустой список `()`.
Судя по тому, что это список, он должен означать вызов функции.
Вот только в нём нет ни аргументов, ни самой функции.
Такая запись в Scheme запрещена и является синтаксической ошибкой.

Поэтому необходимо очень аккуратно анализировать программу и автоцитировать только те данные,
для которых это явно стоить делать, например: числа, строки, логические значения.
([См. далее](#Цитирование) про специальную форму `quote`.)
Так что мы записываем следующее:

```scheme
(define (evaluate exp env)
  (if (atom? exp)
      (cond ((symbol? exp) (lookup exp env))
            ((or (number? exp) (string? exp) (char? exp)
                 (boolean? exp) (vector? exp) )
             exp )
            (else (wrong "Cannot evaluate" exp)) )
      ...
) )
```

<a id="indexR:ошибки!варианты обработки"></a>
<a id="indexC:wrong"></a>
В этом фрагменте кода виден первый случай, когда могут возникнуть ошибки.
Большая часть лисп-систем имеет свои собственные механизмы обработки исключительных ситуаций,
которые непросто сделать переносимыми.
В случае ошибки мы вызываем `wrong`[^4] и передаём ей первым аргументом строку.
В строке находится текстовое описание ошибки,
а следующие за ней аргументы несут дополнительную информацию о том, что вызвало проблему.
Системы с зачаточным механизмом обработки ошибок обычно в случае проблем
выдают какие‑то непонятные надписи вроде `"Bus error: core dumped"` и умирают.
Другие останавливают текущие вычисления и возвращаются к диалоговому режиму.
А третьи могут связывать с вычисляемым выражением специальный обработчик исключений,
который перехватит брошенный объект, описывающий ошибку,
и будет уже по нему решать, что делать дальше.
\seePage[compilation/sect:exception]
В некоторых случаях даже реализуется подобие экспертной системы,
которая анализирует ошибку, вызвавший её код и выдаёт пользователю варианты её исправления.
В общем, сложно сказать однозначно, что следует делать в случае ошибки.

[^4]: Заметьте, не «функцию `wrong`».
      Варианты её реализации подробнее рассматриваются в разделе \ref{compilation/sect:exception}.

<a id="basics/sect:evaluating-forms"></a>
## Вычисляем формы

<a id="indexR:специальные формы"></a>
<a id="indexR:форма!специальная"></a>
<a id="indexR:Лисп!специальные формы"></a>
Каждый язык имеет некоторое количество «неприкасаемых» синтаксических
конструкций: их нельзя адекватно переопределить и вообще не стоит трогать.
В Лиспе такие конструкции называются _специальными формами_.
Они
представляются списками, где первый элемент — это определённый символ,
принадлежащий множеству _специальных операторов_ (или синтаксических
ключевых слов, как их называют в Scheme).

<a id="indexR:функции!примитивы"></a>
<a id="indexR:примитивы"></a>
<a id="indexR:диалекты Лиспа"></a>
Конкретный диалект Лиспа характеризуется набором специальных форм и библиотекой
примитивных функций (эти функции нельзя определить на самом диалекте, так как
они тесно связаны с реализацией; например, для Scheme это `call/cc`).

В некотором понимании, Лисп является лишь прикладным $\lambda$-исчислением
вместе с расширяющим его набором специальных форм.
Специфика каждого конкретного
диалекта Лиспа лежит только в этом наборе.
В Scheme используется минимальный
набор специальных операторов (`quote`, `if`, `set!` и `lambda`),
тогда как {\CommonLisp} (CLtL2 [[ste90]](zz_bibliography.html#ste90)) определяет более тридцати, описывая
таким образом случаи, когда может быть сгенерирован высокоэффективный машинный
код.

Так как специальные формы записываются буквально, то их синтаксический анализ
прост, хватит одного `case`-выражения: надо лишь смотреть на первый элемент
списка.
Если форма начинается не со специального оператора, то она означает
применение функции.
В данный момент мы ограничимся лишь небольшим подмножеством
специальных форм: `quote`, `if`, `begin`, `set!` и `lambda`.
(В следующих главах мы введём другие, более специализированные формы.)

\ForLayout{display}{\begingroup
\lstset{aboveskip=\smallskipamount, belowskip=\smallskipamount}}

<a id="indexC:evaluate"></a>
```scheme
(define (evaluate e env)
  (if (atom? e)
      (cond ((symbol? e) (lookup e env))
            ((or (number? e)(string? e)(char? e)
                 (boolean? e)(vector? e) ) e)
            (else (wrong "Cannot evaluate" e)) )
      (case (car e)
        ((quote)  (cadr e))
        ((if)     (if (evaluate (cadr e) env)
                      (evaluate (caddr e) env)
                      (evaluate (cadddr e) env) ))
        ((begin)  (eprogn (cdr e) env))
        ((set!)   (update! (cadr e) env (evaluate (caddr e) env)))
        ((lambda) (make-function (cadr e) (cddr e) env))
        (else     (invoke (evaluate (car e) env)
                          (evlis (cdr e) env) )) ) ) )
```

\ForLayout{display}{\endgroup}

<a id="indexR:синтаксис!if@\protect`if`"></a>
Чтобы упростить определение, синтаксический анализ оставлен минимальным: мы
не проверяем, правильно ли записаны цитаты, действительно ли `if` передано
три аргумента\footnote{`if` не обязательно принимает условие и ровно две
альтернативы.
К примеру, в Scheme и {\CommonLisp} `if` может принимать как
два, так и три аргумента; `if` в {\EuLisp} и {\ISLisp} исключительно
тернарный; {\LeLisp} в случае, если условие ложно, вычисляет остаток
формы `if`, обернув его в `begin`.} и так далее.
Мы априори считаем
интерпретируемые программы синтаксически корректными.


<a id="basics/evaluating-forms/ssect:quoting"></a>
### Цитирование

<a id="indexC:quote"></a>
<a id="indexE:M-выражения"></a><a id="indexE:S-выражения"></a>
<a id="indexR:программы!как данные"></a><a id="indexR:значения!как программы"></a>
<a id="indexR:цитаты"></a>
Специальная форма `quote` позволяет записать значение, которое без
цитирования было бы спутано с каким-нибудь выражением.
Такой механизм необходим,
если программы тоже являются типом данных в языке — надо же как‑то
различать, где записана программа, а где данные.
Если бы был выбран другой
синтаксис, то эта проблема бы не возникла.
К примеру, изначально в Лиспе
планировались M-выражения [[mcc60]](zz_bibliography.html#mcc60) для записи действий над данными, а сами
данные должно были записываться S-выражениями.
Это решило бы проблему
разделения кода и данных, но сделало бы невозможными вменяемые макросы —
очень полезную вещь для расширения синтаксиса.
Как бы то ни было, M-выражения
долго не прожили [[mcc78a]](zz_bibliography.html#mcc78a), программы и данные стали записываться
исключительно S-выражениями.
Поэтому в Лиспе есть специальная форма `quote`,
служащая для разделения кода и данных.

Суть цитирования состоит в возврате выражения, следующего за ключевым словом,
«как есть», без его вычисления.
Это чётко видно в следующем фрагменте кода:

```scheme
...
(case (car e)
      ((quote) (cadr e)) ...
) ...
```

<a id="indexR:цитаты!явные и неявные"></a>
Также интересен вопрос: а есть ли разница между явным и неявным цитированием,
например, между `33` и `'33`, или между\footnote{Напомним: в Scheme
запись `\#(...)` означает процитированный вектор.} `\#(fa do sol)` и
`'\#(fa do sol)`? Если первое сравнение происходит между непосредственными
значениями и тут вроде бы всё очевидно, то во втором случае сравниваются
составные объекты (хоть для Лиспа они и являются формально атомами).
Можно легко
придумать несколько возможных трактовок данного выражения.
Цитирование
возвращает аргумент как значение, но сама запись `\#(fa do sol)` могла бы
возвращать каждый раз новый вектор из трёх символов.
Другими словами, \ic{\#(fa
do sol)} могла бы быть просто сокращённой записью выражения \ic{(vector 'fa 'do
'sol)}, которое, конечно же, будет означать совсем иное, нежели \ic{'\#(fa do
sol)}, и тем более `(vector fa do sol)`.
Мы вернёмся к этой проблеме позже
\seePage[assignment/sect:quotation], потому что, как видите, не так просто
определиться, какой именно смысл придавать цитированию.


<a id="basics/evaluating-forms/ssect:alternatives"></a>
### Ветвление

<a id="indexC:if"></a>
Вспомним, как работает условный оператор `if`: эта форма вычисляет свой
первый аргумент (_условие_), затем в зависимости от результата вычислений
выбирает, возвращать значение второго аргумента (_следствия_) или третьего
(_альтернативы_).
Эта идея выражается следующим кодом:

```scheme
...
(case (car e)
      ((if) (if (evaluate (cadr e) env)
                (evaluate (caddr e) env)
                (evaluate (cadddr e) env) )) ...
) ...
```

<a id="indexR:представление!логических значений"></a>
<a id="indexR:логические значения"></a>
Эта программа не совсем точно учитывает представление логических значений.
Явно
видно, что здесь смешиваются два языка: Scheme (или хотя бы что‑то неотличимо
похожее на него) и Scheme (или что‑то довольно похожее).
Причём мы определяем
второй в терминах первого.
Так что между ними присутствуют примерно те же
отношения, что и между Паскалем (на котором написана первая реализация \TeX) и
самой системой {\TeX} [[knu84]](zz_bibliography.html#knu84).
Соответственно, нет ни единого повода
считать способы представления логических значений в этих языках одинаковыми.

Функция `evaluate` возвращает значения определяемого языка.
Они априори никак
не связаны с логическими значениями языка, используемого для реализации.
Следуя
соглашению о том, что любой объект, не равный логической _лжи_, должен
считаться логической _истиной_, мы запишем:

```scheme
...
(case (car e)
      ((if) (if (not (eq? (evaluate (cadr e) env) the-false-value))
                (evaluate (caddr e) env)
                (evaluate (cadddr e) env) )) ...
) ...
```

<a id="indexC:the-false-value"></a>
Здесь считается, что переменная `the-false-value` хранит значение _лжи_
определяемого языка, выраженное в терминах определяющего.
Вариантов выбора
такого представления много, например, сделаем так:

```scheme
(define the-false-value (cons "false" "boolean"))
```

В этом случае всё будет замечательно, так как `eq?` в Scheme сравнивает
не значения, а адреса, и, соответственно, любое другое значение никак нельзя
будет спутать со значением переменной `the-false-value`.

<a id="indexC:()"></a><a id="indexC:NIL"></a><a id="indexR:пустой список, \protect`()`"></a>
Вопрос представления логических значений вовсе не тривиален.
В истории Лиспа
полно споров на тему различий между булевым значением _ложь_, пустым
списком `()` и символом `NIL`.
Наиболее чёткая позиция по этому вопросу:
_ложь_ это не `()` (в конце концов, это всего лишь пустой список), и они
оба тем более не имеют никакого отношения к символу, составленному из букв
`N`, `I` и `L`.

Такую позицию занимает и Scheme; об этом всё же смогли договориться за пару
недель до принятия стандарта IEEE [[iee91]](zz_bibliography.html#iee91).

Так что в Scheme теперь всё хорошо (разве что `()` по-английски всё ещё
читается как *nil*!).
В изначальном Лиспе _ложь_, `()` и `NIL`
 — это один и тот же символ.
В {\LeLisp} `NIL` это переменная со
значением `()`, а пустой список (вместе с пустым символом `||`)
используется в качестве _лжи_.


<a id="basics/evaluating-forms/ssect:sequence"></a>
### Последовательность

<a id="indexC:begin"></a><a id="indexC:progn"></a>
Существует специальная форма, позволяющая вычислить группу форм последовательно
и в определённом порядке.
Как и старые добрые блоки `begin ...
end` из
семейства языков Алгола, в Scheme эта форма называется `begin`; в других
Лиспах она обычно зовётся `progn` — обобщенная версия `prog1`,
`prog2` {\itd} Собственно организацию последовательности мы перепоручаем
функции `eprogn`.

<a id="indexC:eprogn"></a>
```scheme
...
(case (car e)
      ((begin) (eprogn (cdr e) env)) ...
) ...

(define (eprogn exps env)
  (if (pair? exps)
      (if (pair? (cdr exps))
          (begin (evaluate (car exps) env)
                 (eprogn (cdr exps) env) )
          (evaluate (car exps) env) )
      '() ) )
```

Такое определение последовательных вычислений не допускает разночтений.
Стоит
обратить внимание на хвостовую рекурсию при вычислении последней формы
последовательности.
Вычисления построены так, что последний вызов `evaluate`
заменяет собой всю рекурсивную цепочку вызовов `eprogn`.
(О хвостовой
рекурсии мы позже поговорим подробнее.
\seePage[escape/pr-cont/ssect:tail-recusion])

<a id="indexC:begin!возвращаемое значение"></a>
<a id="indexR:возвращаемые значения!формы (begin)@формы \protect`(begin)`"></a>
<a id="indexC:empty-begin"></a>
Ещё одним интересным моментом является то, чт\'{о} возвращается при вычислении
формы `(begin)`.
Сейчас это пустой список.
Но почему именно `()`, почему
не что‑то другое, вроде `:3` или `(\nya)`? Мы выбрали пустой список по
привычке, доставшейся в наследство от Лиспа: в любой непонятной ситуации
возвращай `nil`.
Но в мире, где _ложь_, `nil` и `()` — это
совершенно различные вещи, что из них лучше подходит на роль *ничего*?
Поэтому пусть в нашем языке вычисление `(begin)` будет возвращать специальное
значение `empty-begin`, которое определяется как (почти) случайное число
`813` [[leb05]](zz_bibliography.html#leb05).

<a id="indexC:eprogn"></a><a id="indexC:empty-begin"></a>
```scheme
(define (eprogn exps env)
  (if (pair? exps)
      (if (pair? (cdr exps))
          (begin (evaluate (car exps) env)
                 (eprogn (cdr exps) env) )
          (evaluate (car exps) env) )
      empty-begin ) )

(define empty-begin 813)
```

<a id="indexR:синтаксис!begin@`(begin)`"></a>
<a id="indexC:unspecified"></a>
Корни нашей проблемы в том, что `begin` *обязана* вернуть какое‑то
значение.
Как и Scheme, определяемый язык может не придавать какого-либо смысла
форме `(begin)`; мы можем или допускать такое написание и возвращать любое
удобное значение, или же не допускать и считать ошибкой.
Идеальный вариант:
вообще не использовать `begin` без аргументов, так как не определено, что
именно получится в результате.
Некоторые реализации имеют специальный объект:
`\#<unspecified>`, который возвращается в случае, когда нет ничего более
подходящего.
Обычно единственное, что с ним можно сделать — это вывести на
печать.
(Не следует путать этот объект с псевдозначением у неопределённых
переменных.
\seePage[lisp1-2-omega/recusion/ssect:uninitialized])

\bigskip

<a id="indexR:язык!чисто функциональный"></a>
<a id="indexR:побочные эффекты"></a>
Последовательное вычисление выражений бесполезно в чисто функциональных языках
(где функции не имеют побочных эффектов).
Действительно, какой в таком случае
смысл что‑то вычислять и не использовать возвращаемые значения? Но иногда
в этом может быть смысл.
Представим себе игру, написанную на чисто
функциональном языке; очевидно, что вычисления занимают какое‑то время
вне зависимости от того, используются ли их результаты или нет; и нас может
интересовать именно этот «побочный эффект» — замедление работы, — а
не получаемые результаты.
Тогда можно последовательно что‑то вычислять,
например, чтобы скорость игры была адекватна рефлексам игрока (если только
компилятор не посчитает себя самым умным и не удалит «бесполезный» код).

<a id="indexC:begin!необходимость"></a>
Так как в Scheme есть операции ввода-вывода, которые имеют побочные эффекты, то
для нас есть смысл пользоваться формой `begin`, потому как очевидно, что
лучше сначала задать вопрос (с помощью `display`), а потом прочитать ответ
(с помощью `read`), чем сделать наоборот.
Здесь‑то как раз и нужно
упорядочить вычисления.
Но не только `begin` может их упорядочивать.
Например, условный оператор может:

```scheme
(if |$\alpha$| |$\beta$| |$\beta$|) |\eq| (begin |$\alpha$| |$\beta$|)
```

\phantomlabel{basics/forms/sequence/par:gensym-puzzle}
\noindent
И `lambda` тоже может\footnote{Переменная *void* не должна быть свободной
в $\beta$.
Это условие выполняется, если *void* никогда не встречается
в $\beta$.
Обычно в таком случае используется `gensym`, чтобы получить
гарантированно уникальное имя переменной.
\seeEx[basics/ex:no-gensym]}:

```scheme
(begin |$\alpha$| |$\beta$|) |\eq| ((lambda (|*void*|) |$\beta$|) |$\alpha$|)
```

Как видно из этого примера, в Scheme `begin` не является необходимой
специальной формой, так как её поведение можно проэмулировать с помощью
функций благодаря тому, что при вызове функции её аргументы вычисляются перед
исполнением тела (передача аргументов _по значению_).


<a id="basics/evaluating-forms/ssect:assignment"></a>
### Присваивание

<a id="indexC:set"!"></a>
<a id="indexR:присваивание"></a>
<a id="indexR:соглашения именования!побочных эффектов"></a>
Как и во многих других языках, в нашем диалекте значения переменных можно
менять.
Изменение значения переменной называется _присваиванием_.
Так как
значение переменной надо изменять в её окружении, то мы оставляем эту проблему
функции `update!`.\footnote*{В соответствии с принятым в Scheme соглашением,
имена функций с побочными эффектами оканчиваются на восклицательный знак.} Её
настоящая суть объясняется позже, на
странице [assignment/implementation/ssect:environment](xxx_appropriate.html#assignment/implementation/ssect:environment).

```scheme
...
(case (car e)
      ((set!) (update! (cadr e) env
                       (evaluate (caddr e) env))) ...
) ...
```

Присваивание выполняется в два шага: сначала вычисляется новое значение, потом
новое значение заменяет старое.
Стоит заметить, что обновлённая переменная
не является значением данной формы.
Мы ещё вернёмся к вопросу о возвращаемом
значении операции присваивания.
\seePage[chapter:assignment] Пока только
запомните, что стандартом оно не определено.


<a id="basics/evaluating-forms/ssect:abstraction"></a>
### Абстракция

<a id="indexR:абстракция"></a>
Функции (также называемые _процедурами_ в Scheme) являются результатом
вычисления специальной формы `lambda`, чьё имя ссылается на понятие
_абстракции_ в $\lambda$-исчислении.
Работу по созданию функции мы
поручаем функции `make-function`, которой передаём всё необходимое: список
аргументов, тело функции и текущее окружение.

```scheme
...
(case (car e)
      ((lambda) (make-function (cadr e) (cddr e) env)) ...
) ...
```


<a id="basics/evaluating-forms/ssect:application"></a>
### Аппликация

<a id="indexR:аппликация"></a>
<a id="indexR:комбинация"></a>
<a id="indexR:терм!функциональный"></a>
<a id="indexR:функциональный терм"></a>
Если первый элемент списка не является специальным оператором, то такой список
означает применение функции, которое в $\lambda$-исчислении называется
_аппликацией_ или _комбинацией_.
Функция, полученная в результате
вычисления первого элемента, применяется к аргументам, которые мы получим,
вычислив остальные элементы списка.
Эти действия описываются следующим кодом:

```scheme
...
(case (car e)
      (else (invoke (evaluate (car e) env)
                    (evlis (cdr e) env) )) ) ...
```

Вспомогательная функция `evlis` принимает список выражений и возвращает
список соответствующих им значений.
Она определяется следующим образом:

<a id="indexC:evlis"></a>
```scheme
(define (evlis exps env)
  (if (pair? exps)
      (cons (evaluate (car exps) env)
            (evlis (cdr exps) env) )
      '() ) )
```

Далее работает функция `invoke`, которая применяет свой первый аргумент
(функцию; если это не так, она сообщает об ошибке) ко второму (списку её
аргументов) и возвращает результат вычислений.
В общем, `invoke` похожа на
привычную `apply`, разве что требует явного указания окружения.
(Далее
в разделе \ref{basics/sect:representing-functions}
\seePage[basics/sect:representing-functions] мы рассмотрим подробнее непростые
взаимоотношения функций и окружений.)


\subsubsection{\texorpdfstring{Ещё немного об \protect`evaluate`}<!--  -->
{Ещё немного об evaluate}}

<a id="indexR:порядок вычислений!термов аппликации"></a>
Рассмотренное описание языка является более-менее точным.
Мы не разобрали лишь
несколько вспомогательных функций: `lookup` и `update!`, отвечающие за
окружения, и `make-function` вместе с `invoke`, занимающиеся функциями.
Но даже сейчас нам уже многое известно об `evaluate`.
Например, наш диалект
имеет единое пространство имён, понятие объекта в нём распространяется вообще
на всё (как в \Lisp1 \seePage[chapter:lisp1-2-omega]), в том числе и на функции.
Но мы до сих пор не знаем порядок вычисления аргументов.

В нашем случае он зависит от порядка вычисления аргументов у `cons`, которая
используется в `evlis`.
Но мы легко можем указать любой понравившийся нам
порядок, например, слева направо:

```scheme
(define (evlis exps env)
  (if (pair? exps)
      (let ((argument1 (evaluate (car exps) env)))
        (cons argument1 (evlis (cdr exps) env)) )
      '() ) )
```

Без введения новых специальных конструкций\footnote{Как известно, `let` —
это всего лишь простой макрос: `(let (($x$ $\pi_1$)) $\pi_2$)` {\eq}
`((lambda ($x$) $\pi_2$) $\pi_1$)`.} мы уточнили поведение определяемого
языка.
Первая часть книги направлена именно на это: уточнение определяемого
языка с помощью всё более ограниченных возможностей, что снижает зависимость
описания от языка, используемого для определения.


<a id="basics/sect:repr-the-env"></a>
## Представление окружений

<a id="indexE:A-список"></a><a id="indexR:А-список"></a>
<a id="indexR:ассоциативный список"></a>
Окружения связывают переменные с их значениями.
Обычно в Лиспе подобные связи
представляются _ассоциативными списками_, также называемыми
_А-списками_.
Мы тоже будем представлять окружения как А-списки,
связывающие переменные и значения.
Для простоты имена переменных будем
представлять символами.

Таким образом, функции `lookup` и `update!` определяются элементарно:

\ForLayout{display}{\begingroup
\lstset{aboveskip=\smallskipamount, belowskip=\smallskipamount}}

<a id="indexC:lookup"></a>
```scheme
(define (lookup id env)
  (if (pair? env)
      (if (eq? (caar env) id)
          (cdar env)
          (lookup id (cdr env)) )
      (wrong "No such binding" id) ) )
```

Тут мы видим второй тип\footnote{Первый — это синтаксические ошибки
(стр. [basic/atoms/para:the-first-error](xxx_appropriate.html#basic/atoms/para:the-first-error)).} возможных ошибок,
появляющихся при попытке узнать значение неизвестной переменной.
Мы опять лишь
вызовем `wrong`, чтобы сообщить о проблеме наверх.

<a id="indexR:автоцитирование"></a>
Когда компьютеры были большими, а память была маленькой,\footnote*{Память
(вместе с подсистемами ввода-вывода) всё ещё остаётся одной из наиболее дорогих
частей компьютера, хоть и постоянно дешевеет.} для переменных часто применялось
_автоцитирование_.
Если с переменной не было связано какое-либо значение,
то этим значением становился символ с именем переменной.
Было бы очень обидно
видеть, как понятия переменной и символа, которые мы так усердно разделяли,
опять смешиваются и перепутываются.

Хотя это несомненно удобно — никогда не вызывать ошибок, но такой идеальный
мир имеет большой недостаток: задача программы не в том, чтобы работать без
ошибок, а в том, чтобы выполнять то, для чего она предназначена.
В этом смысле
ошибки играют роль перил: если мы на них натыкаемся, то это значит, мы идём
куда‑то не туда.
Ошибки должны быть обнаружены как можно раньше, чтобы как
можно быстрее их исправить.
Следовательно, использование автоцитирования —
плохое решение, потому что оно скрывает некоторые ошибки, которые могли бы быть
исправлены раньше.

Функция `update!` изменяет окружение, так что, скорее всего, тоже может
вызвать такую же ошибку: нельзя изменить значение неизвестной переменной.
Мы
обсудим, следует ли ей так поступать, когда будем говорить о глобальном
окружении.

<a id="indexC:update"!"></a>
```scheme
(define (update! id env value)
  (if (pair? env)
      (if (eq? (caar env) id)
          (begin (set-cdr! (car env) value)
                 value)
          (update! id (cdr env) value) )
      (wrong "No such binding" id) ) )
```

\ForLayout{display}{\endgroup}

<a id="indexC:set"!!возвращаемое значение"></a>
<a id="indexR:присваивание!возвращаемое значение"></a>
<a id="indexR:возвращаемые значения!присваивания"></a>
Возвращаемое значение функции `update!` выбрано с учётом того, что это
значение станет значением всего выражения присваивания.
В Scheme это значение
не определено.
Строго говоря, программам не следует полагаться на какое‑то
значение, но, тем не менее, мы вынуждены выбирать, что именно будем возвращать
в нашем случае.
Например:

\begin{enumerate}
  \item только что присвоенное значение (так сделано сейчас);

  \item предыдущее значение переменной (могут быть проблемы
        с инициализацией, первым присваиванием);

  \item <a id="indexC:UFO"></a>\label{basics/s:env/ufo}
        объект «неопределённое значение», некий `\#<UFO>`,
        используемый исключительно как индикатор неопределённого
        значения;

  \item значение формы с неопределённым значением, вроде
        `set-cdr!` в Scheme.
\end{enumerate}

<a id="indexR:окружение!как тип данных"></a>
Окружения — это составной абстрактный тип данных.
Мы уже можем извлекать и
изменять их части с помощью соответствующих функций; но ещё надо уметь
создавать новые окружения и добавлять в них новые части.

<a id="indexR:окружение!начальное"></a>
Изначально в окружении ничего нет.
Это записывается просто:

<a id="indexC:env.init"></a>
```scheme
(define env.init '())
```

(Чуть позже, в разделе \ref{basics/sect:representing-functions}, мы сделаем его
не таким необитаемым.)

Когда вызывается функция, для неё создаётся новое окружение, в котором её
аргументы связаны со своими фактическими значениями.
Функция `extend`
расширяет окружение `env` переменными `variables` с соответствующими
значениями `values`.

<a id="indexC:extend"></a>
```scheme
(define (extend env variables values)
  (cond ((pair? variables)
         (if (pair? values)
             (cons (cons (car variables) (car values))
                   (extend env (cdr variables) (cdr values)) )
             (wrong "Too few values") ) )
        ((null? variables)
         (if (null? values)
             env
             (wrong "Too many values") ) )
        ((symbol? variables) (cons (cons variables values) env)) ) )
```

<a id="indexR:список аргументов"></a>
<a id="indexR:синтаксис!списка аргументов"></a>
<a id="indexR:переменные!точечные"></a>
<a id="indexR:точечные переменные"></a>
Главная сложность состоит в том, что нам надо проанализировать все варианты
записи <*списка аргументов*>, какие разрешены в Scheme.\footnote*{Некоторые
лисп-системы, вроде \CommonLisp, поддались соблазну расширить синтаксис перечня
аргументов ключевыми словами вроде `\&aux`, `\&key`, `\&rest`.
Но это
сильно усложняет синтаксический разбор и связывание.
Иные системы позволяют даже
обобщённое указание аргументов с помощью шаблонов [[sj93]](zz_bibliography.html#sj93).} Список
аргументов может быть представлен не только обычным списком символов, но и
точечным: заканчивающимся не на `()`, а на определённый символ
(_точечную переменную_).
Более формально список аргументов описывается
следующей грамматикой:

\begin{center}\setlength{\tabcolsep}{2pt}\begin{tabular}{rcl}
  <*список аргументов*> &  ::=  & `()`                                  \\
                           &   |   & <*переменная*>                        \\
                           &   |   & \ic{({\normalfont<*переменная*>}
                                    .
{\normalfont<*список аргументов*>})} \\
         <*переменная*> & $\in$ & \Vset{Символы}
\end{tabular}\end{center}

Когда мы расширяем окружение, количество значений переменных должно
соответствовать количеству их имён.
Обычно их поровну, но если список
оканчивается на точечную (или _n‑арную_) переменную, то она связывается
со списком всех оставшихся аргументов.
Как бы то ни было, возможны две ошибки:
или значений больше, чем переменных, или наоборот.


<a id="basics/sect:representing-functions"></a>
## Представление функций

Наверное, проще всего представлять функции с помощью функций.
Естественно, это
не тавтология, и читать следует так: «функции определяемого языка проще всего
представлять функциями языка определения».
Это сильно упрощает механизм вызова:
функция `invoke` должна лишь проверить, действительно ли её первый аргумент
является функцией: чем‑то, что можно вызвать.

<a id="indexC:invoke"></a>
```scheme
(define (invoke fn args)
  (if (procedure? fn) (fn args)
      (wrong "Not a function" fn) ) )
```

Проще некуда.
Даже может возникнуть вопрос, зачем вообще нужна отдельная
функция, когда весь этот код можно было встроить сразу в `evaluate`.
Причина,
по которой так сделано, в том, что мы показываем структуру будущих
интерпретаторов, а в них `invoke` будет уже не такой простой.
Кстати,
попробуйте сейчас выполнить упражнения \ref{basics/ex:def-call/cc}
и \ref{basics/ex:def-apply}.

<a id="indexR:аппликация!порядок вычисления термов"></a>
<a id="indexR:порядок вычислений!термов аппликации"></a>
Также у нас появляется новый тип ошибок, возникающих при попытке вызвать
невызываемое.
Сейчас мы обрабатываем такие ошибки в момент применения функции
к уже вычисленным аргументам, но мы могли бы предупреждать пользователя раньше.
В таком случае нам необходимо задать порядок вычисления элементов формы вызова
функции:

\begin{enumerate}
  \item вычислить элемент на месте функции;

  \item если это не функция, сообщить об ошибке;

  \item вычислить аргументы слева направо;

  \item сравнить количество аргументов с арностью функции
        и, если они не совпадают, то сообщить об ошибке.
\end{enumerate}

Вычислять аргументы слева направо кажется логичным для людей, читающих слева
направо.
Это и запрограммировать легче, в итоге порядок прост и понятен.
Сложности возникают только у компилятора, потому что если ему захочется поменять
этот порядок (например, чтобы эффективнее использовать регистры процессора), то
он будет вынужден доказать, что это не изменит смысла программы.

Конечно, мы могли бы действовать эффективнее, проверяя арность ещё раньше:

\begin{enumerate}
  \item вычислить элемент на месте функции;

  \item если это не функция, сообщить об ошибке, иначе запомнить ожидаемое
        количество аргументов;

  \item вычислять аргументы слева направо до тех пор, пока их количество
        согласуется с арностью функции, в случае проблем сообщить об ошибке;

  \item применить функцию к аргументам.\footnote*{Функция могла бы потом
        ещё проверять на правильность типы переданных аргументов, но это
        не имеет отношения к механизму вызова.}
\end{enumerate}

Стандарт {\CommonLisp} требует, чтобы аргументы вычислялись строго слева
направо, но с целью оптимизации позволяет вычислять функциональный элемент
списка до или после остальных.

Scheme же не накладывает условий на порядок вычисления всех элементов формы
вызова функции, включая сам элемент-функцию.
Так как ограничений нет, то
компилятор волен выбирать любой устраивающий его порядок.
\seePage[denotational/sect:eval-order] А пользователь, в свою очередь, не может
рассчитывать на какой-либо определённый порядок вычислений и должен использовать
`begin`, чтобы задать необходимый порядок явно.

Считается плохим стилем использовать вызовы функций, чтобы получить побочные
эффекты в нужной последовательности.
Поэтому следует избегать выражений вроде
`(f (set! f $\pi$) (set! f $\pi'$))`, где неясно, какая же функция будет
вызвана на самом деле.
Ошибки, возникающие в подобных случаях, очень сложно
отлавливать.


\subsection*{Окружение исполнения функций}

<a id="indexR:окружение!исполнения тела функции"></a>
Применение функции сводится к вычислению выражений, составляющих её тело,
в окружении, где аргументы функции связаны со значениями, переданными при вызове
функции.
Вспомните, что при вызове `make-function` мы передали всё
необходимое для этого, находящееся в распоряжении `evaluate`.
В оставшейся
части этого раздела мы будем разбирать используемые при вычислениях окружения,
в программах они будут набраны *курсивом*.


\subsubsection{Минимальное окружение}

Для начала рассмотрим минимально возможное окружение:

<a id="indexC:make-function"></a>
```scheme
(define (make-function variables body env)
  (lambda (values)
    (eprogn body (extend |*env.init*| variables values)) ) )
```

\indexE{K@\protect\comb{K}, комбинатор}
\indexR{комбинаторы!K@\protect\comb{K}}
В строгом соответствии с ранее описанным соглашением, тело функции вычисляется
в окружении, где аргументы функции связаны с переданными значениями.
Например,
получив в результате вызова `make-function` комбинатор \comb{K}, определяемый
как `(lambda (a b) a)`, мы можем его вызвать следующим образом:

```scheme
(K 1 2) |\is| 1
```

Но есть и неприятность: функция может использовать только свои аргументы и
локальные переменные, потому что мы определили *env.init* как пустое
окружение.
В нём нет даже базовых функций из глобального окружения вроде
`car` или `cons`.


\subsubsection{Улучшенное окружение}

Хорошо, попробуем улучшить наше определение следующим образом:

```scheme
(define (make-function variables body env)
  (lambda (values)
    (eprogn body (extend |*env.global*| variables values)) ) )
```

Замечательно, теперь наши функции имеют доступ к глобальному окружению и всем
его функциям.
А что если мы попробуем определить взаимно рекурсивные функции?
Также, какой результат даст программа слева (справа она же с раскрытыми
макросами)?

{\def\E{\hbox to 0pt{\kern0.3em$\equals$}}
```scheme
(let ((a 1))            ((lambda (a)
  (let ((b (+ 2 a)))       ((lambda (b)
    (list a b) ) )   |\E|         (list a b) )
                            (+ 2 a) ) )
                         1 )
```}

Давайте рассмотрим по шагам, как вычисляется это выражение:

```scheme
((lambda (a) ((lambda (b) (list a b)) (+ 2 a))) 1)|\begin{where}
                                                   \- *env.global*
                                                   \end{where}|
|\equals| ((lambda (b) (list a b)) (+ 2 a))|\begin{where}
                                            \- a {\is} 1
                                            \- *env.global*
                                            \end{where}|
|\equals| (list a b)|\begin{where}
                     \- b {\is} 3
                     \- *env.global*
                     \end{where}|
```

Тело внутренней функции `(lambda (b) (list a b))` выполняется в окружении,
полученном расширением глобального окружения переменной `b`.
Всё верно.
Но
в этом окружении нет необходимой переменной `a`!


\subsubsection{Улучшенное окружение (вторая попытка)}

Так как нам надо видеть переменную `a` во внутренней функции, то достаточно
будет передать `invoke` текущее окружение, а она в свою очередь передаст его
вызываемой функции.
Чтобы реализовать эту идею, надо немного подправить
`evaluate` и `invoke`; чтобы не путать эти определения с предыдущими,
пусть они начинаются на `d.`:

<a id="indexC:d.evaluate"></a>
<a id="indexC:d.invoke"></a>
<a id="indexC:d.make-function"></a>
```scheme
(define (d.evaluate e env)
  (if (atom? e) ...
      (case (car e)
        ...
        ((lambda) (d.make-function (cadr e) (cddr e) env))
        (else     (d.invoke (d.evaluate (car e) env)
                            (evlis (cdr e) env)
                            env )) ) ) )

(define (d.invoke fn args env)
  (if (procedure? fn)
      (fn args env)
      (wrong "Not a function" fn) ) )

(define (d.make-function variables body |*def.env*|)
  (lambda (values |*current.env*|)
    (eprogn body (extend |*current.env*| variables values)) ) )
```

В этом определении стоит заметить, что передача окружения определения `env`
через переменную *def.env* бессмысленна, так как при вызове используется лишь
текущее окружение *current.env*.

<a id="indexR:стек!вызовов"></a>
Давайте теперь ещё раз рассмотрим пример, приведённый выше.
Сейчас переменные
не пропадают:

```scheme
((lambda (a) ((lambda (b) (list a b)) (+ 2 a))) 1)|\begin{where}
                                                   \- *env.global*
                                                   \end{where}|
|\equals| ((lambda (b) (list a b)) (+ 2 a))|\begin{where}
                                            \- a {\is} 1
                                            \- *env.global*
                                            \end{where}|
|\equals| (list a b)|\begin{where}
                     \- b {\is} 3
                     \- a {\is} 1
                     \- *env.global*
                     \end{where}|
```

Заодно мы явно видим *стек вызовов*: каждая связывающая форма сначала
укладывает свои новые переменные поверх текущего окружения, а потом убирает их
оттуда после окончания вычислений.


\subsubsection{Исправляем проблему}

Но даже при таком определении всё ещё есть проблемы.
Рассмотрим следующий
пример:

```scheme
(((lambda (a)
     (lambda (b) (list a b)) )
  1 )
 2 )
```

Функция `(lambda (b) (list a b))` создаётся в окружении, где `a` связана
со значением `1`, но в момент вызова в окружении будет присутствовать
только `b`.
Таким образом, мы опять потеряли переменную `a`.

Без сомнения, вы заметили, что в определении `d.make-function` присутствуют
два окружения: окружение определения *def.env* и окружение исполнения
*current.env*.
В жизни функции есть два важных события: её создание и
её вызов(ы).
Очевидно, что создаётся функция только однажды, а вызываться может
несколько раз; или вообще никогда не вызываться.
Следовательно,
единственное\footnote{На самом деле, здесь можно использовать любое необходимое
окружение.
См. про форму `closure` на
странице [assignement/assignement/para:closure](xxx_appropriate.html#assignement/assignement/para:closure).} окружение, которое мы
однозначно можем связать с функцией, — это окружение, в котором она была
создана.
Вернёмся к исходным определениям функций `evaluate` и `invoke`,
но в этот раз функцию `make-function` запишем следующим образом:

<a id="indexC:make-function"></a>
```scheme[label=basics/representing-functions/fixing/src:inject-current-env]
(define (make-function variables body |*env*|)
  (lambda (values)
    (eprogn body (extend |*env*| variables values)) ) )
```

Теперь все приведённые примеры работают нормально.
В частности, пример выше
вычисляется следующим образом:

```scheme
(((lambda (a) (lambda (b) (list a b))) 1) 2)|\begin{where}
                                             \- *env.global*
                                             \end{where}|
|\equals| ((lambda (b) (list a b))|\begin{where}
                                   \- a {\is} 1
                                   \- *env.global*
                                   \end{where}|
    2 )|\begin{where}
        \- *env.global*
        \end{where}|
|\equals| (list a b)|\begin{where}
                     \- b {\is} 2
                     \- a {\is} 1
                     \- *env.global*
                     \end{where}|
```

<a id="indexR:абстракция!замыкание"></a>
<a id="indexR:абстракция!значение"></a>
<a id="indexR:возвращаемые значения!абстракций"></a>
<a id="indexR:замыкания (closures)"></a>
Форма `(lambda (b) (list a b))` создаётся в глобальном окружении, расширенном
переменной `a`.
Когда эта функция вызывается, она расширяет окружение своего
создания переменной `b`, таким образом, тело функции будет вычисляться
в окружении, где обе переменные `a` и `b` присутствуют.
После того, как
функция вернёт результат, исполнение продолжается в глобальном окружении.
Мы
будем называть значение абстракции _замыканием_ (closure), потому что при
создании этого значения тело функции становится замкнутым в окружении своего
определения.

Стоит отметить, что сейчас `make-function` сама использует замыкания языка
определения.
Это не является обязательным, как мы покажем далее в третьей главе.
\seePage[escape/actors/ssect:functions] Функция `make-function` возвращает
замыкания, а это — характерная черта функциональных языков программирования.


\subsection{Динамическая и лексическая области видимости}<!--  -->
\label{basics/representing-functions/ssect:dynamic-and-lexical-binding}

Из этого разговора об окружениях можно сделать два вывода.
Во-первых, ясно,
что с окружениями не всё так просто.
Любое вычисление всегда производится
в каком‑то окружении, следовательно, необходимо эффективно реализовывать их
использование.
В третьей главе рассматриваются более сложные вещи вроде
раскрутки стека и соответствующей формы `unwind-protect`, которые потребуют
от нас ещё более точного контроля над окружениями.

<a id="indexR:лексическое связывание"></a><a id="indexR:динамическое связывание"></a>
<a id="indexR:связывание!лексическое"></a><a id="indexR:связывание!динамическое"></a>
<a id="indexR:Лисп!лексический"></a><a id="indexR:Лисп!динамический"></a>
Второй момент связан с двумя рассмотренными в предыдущем разделе вариантами,
которые являются примерами _лексического_ и \term{динамического
связывания}\footnote{В объектно-ориентированных языках под динамическим
связыванием обычно понимается механизм выбора метода объекта на основе его
реального типа во время исполнения программы, в противоположность статическому
связыванию, при котором метод выбирается компилятором исходя из типа переменной,
которая хранит рассматриваемый объект.} (также применяются термины лексическая и
динамическая область видимости).
В *лексическом* Лиспе функция выполняется
в окружении своего определения, расширенном собственными переменными, тогда как
в *динамическом* — расширяет текущее окружение, окружение своего вызова.

Сейчас в моде лексическое связывание, но это не значит, что у динамического нет
будущего.
С одной стороны, именно динамическое связывание применяется
в некоторых довольно популярных языках вроде \TeX [[knu84]](zz_bibliography.html#knu84),
Emacs Lisp\trnote{Начиная с Emacs Lisp v.\,24 и Perl 5, эти языки имеют и
лексические переменные.} [[llst93]](zz_bibliography.html#llst93), Perl [[ws91]](zz_bibliography.html#ws91).

С другой стороны, сама идея динамической области видимости является важной
концепцией программирования.
Она соответствует установке связей перед
выполнением вычислений и гарантированному автоматическому удалению этих связей
после завершения вычислений.

<a id="indexR:исключения"></a>
<a id="indexR:поиск с возвратом"></a>
Такую стратегию можно эффективно применять, например, в искусственном
интеллекте.
В этом случае сначала выдвигается некая гипотеза, затем из неё
вырабатываются следствия.
Как только система натыкается на противоречие, то
гипотезу следует отвергнуть и перейти к следующей.
Это называется \term{поиском
с возвратом}.
Если следствия гипотез хранятся без использования побочных
эффектов, например, в А-списках, то отвержение гипотезы автоматически и без
проблем утилизирует и все её следствия.
Но если для этого используются
глобальные переменные, массивы {\itd}, то тогда за ненужной гипотезой
приходится долго убирать, вспоминая, каким же было состояние памяти в момент
формулировки гипотезы и какие его части можно откатить до старых значений, чтобы
ничего не сломать! Динамическая область видимости позволяет гарантировать
существование переменной с определённым значением на время и только во время
вычислений, независимо от того, будут они успешны или нет.
Это свойство также
широко используется при обработке исключений.

<a id="indexR:область видимости"></a>
_Область видимости_ переменной — это, можно сказать, географическое
понятие в программе: местность, где переменная встречается и её можно
использовать.
В чистом Scheme (не обременённом полезными, но не абсолютно
необходимыми вещами вроде `let`) есть только одна связывающая форма:
`lambda`.
Это единственная форма, вводящая новые переменные и предоставляющая
им область видимости в рамках определяемой функции.
В динамическом же Лиспе
область видимости в принципе не может быть ограничена функцией.
Рассмотрим
следующий пример:

```scheme
(define (foo x) (list x y))
(define (bar y) (foo 1991))
```

В лексическом Лиспе переменная `y` в `foo`\footnote{О происхождении
*foo* см. [[ray91]](zz_bibliography.html#ray91).} — это всегда ссылка на глобальную
переменную `y`, которая не имеет никакого отношения к `y` внутри `bar`.
В динамическом же Лиспе переменная `y` из `bar` будет видима в `foo`
внутри `bar`, потому что в момент вызова `foo` переменная `y` уже
находилась в текущем окружении.
Следовательно, если мы дадим глобальной `y`
значение `0`, то получим следующие результаты:

```scheme
(define y 0)
(list (bar 100) (foo 3)) |\is| ((1991 0) (3 0))   ; в лексическом Лиспе
(list (bar 100) (foo 3)) |\is| ((1991 100) (3 0)) ; в динамическом Лиспе
```

<a id="indexR:свободные переменные!и области видимости"></a>
Заметьте, что в динамическом Лиспе `bar` понятия не имеет о том, что
в `foo` используется её же локальная переменная `y`, а `foo` не знает
о том, в каком именно окружении следует искать значение своей свободной
переменной `y`.
Просто `bar` при вызове положила в текущее окружение
переменную `y`, а внутренняя функция `foo` нашла её в своём
текущем окружении.
Непосредственно перед выходом `bar` уберёт свою `y` из
окружения, и глобальная переменная `y` снова станет видна.

Конечно, если не использовать свободные переменные, то нет особой разницы между
динамической и лексической областями видимости.

Лексическое связывание получило своё имя потому, что в данном случае достаточно
иметь только код функции, чтобы с уверенностью отнести каждую используемую в ней
переменную к одному из двух классов: или переменная находится внутри связывающей
формы и является локальной, или же это глобальная переменная.
Это чрезвычайно
просто: достаточно взять исходный код, взять карандаш (или мышку) и поставить
его кончик на переменную, значение которой нас интересует, после чего следует
вести карандаш справа налево, снизу вверх до тех пор, пока не встретим первую
связывающую форму.
Динамическое же связывание названо в честь концепции
_динамического времени жизни_ переменных, которую мы будем рассматривать
позже.
\seePage[escape/forms/ssect:dynamic]

Scheme поддерживает только лексические переменные.
{\CommonLisp} поддерживает
оба типа с одинаковым синтаксисом.
Синтаксис {\EuLisp} и {\ISLisp} разделяет эти
два типа переменных, и они находятся в отдельных пространствах имён.
\seePage[lisp1-2-omega/sect:namespaces]

<a id="indexR:область видимости!конфликт имён"></a>
<a id="indexR:переменные!сокрытие имён"></a>
<a id="indexR:сокрытие переменных"></a>
\indexR{сокрытие переменных|seealso{области видимости}}
Область видимости переменной может прерываться.
Такое случается, когда одна
переменная _скрывает_ другую из‑за того, что обе имеют одинаковое имя.
Лексические области видимости вкладываются друг в друга, скрывая переменные
с совпадающими именами из внешних областей.
Этот известный «блокирующий»
порядок разрешения конфликтов унаследован от Алгола 60.

Под влиянием $\lambda$-исчисления, в честь которого названа специальная
форма `lambda` [[per79]](zz_bibliography.html#per79), \LISP 1.0 был сделан динамическим, но вскоре
Джон Маккарти осознал, что он ожидал получить от следующего выражения
`(2 3)`, а не `(1 3)`:

```scheme
(let ((a 1))
  ((let ((a 2)) (lambda (b) (list a b)))
   3 ) )
```

<a id="indexC:function!для замыканий"></a>
<a id="indexC:lambda!как ключевое слово"></a>
Эта аномалия (не осмелюсь назвать её ошибкой) была исправлена введением новой
специальной формы `function`.
Она принимала `lambda`-форму и создавала
_замыкание_ — функцию, связанную с окружением, в котором она определена.
При вызове замыкания вместо текущего окружения расширялось окружение
определения, замкнутое внутри него.
Вместе с изменениями `d.evaluate` и
`d.invoke`, форма `function`\footnote{Наша имитация не совсем точна, так
как существует немало диалектов Лиспа (вроде CLtL1 [[ste84]](zz_bibliography.html#ste84)), где
`lambda` — это не специальный оператор, а только ключевое слово-маркер
вроде `else` внутри `cond` и `case`.
В этом случае `d.evaluate`
может вообще не знать ни о какой `lambda`.
Иногда даже накладываются
ограничения на положение `lambda`-форм, разрешающие им находиться только
внутри `function` и в определениях функций.} выражается так:

<a id="indexC:d.invoke"></a><a id="indexC:d.make-function"></a><a id="indexC:d.make-closure"></a>
```scheme[label=basics/repr-func/dyn-and-lex-bind/src:closure-eval]
(define (d.evaluate e env)
  (if (atom? e) ...
      (case (car e)
        ...
        ((function)   ; Синтаксис: `(function (lambda *аргументы* *тело*))`
         (let* ((f   (cadr e))
                (fun (d.make-function (cadr f) (cddr f) env)) )
           (d.make-closure fun env) ) )
        ((lambda) (d.make-function (cadr e) (cddr e) env))
        (else     (d.invoke (d.evaluate (car e) env)
                            (evlis (cdr e) env)
                            env )) ) ) )

(define (d.invoke fn args env)
  (if (procedure? fn)
      (fn args env)
      (wrong "Not a function" fn) ) )

(define (d.make-function variables body env)
  (lambda (values current.env)
    (eprogn body (extend current.env variables values)) ) )

(define (d.make-closure fun env)
  (lambda (values current.env)
    (fun values env) ) )
```

<a id="indexR:переменные!специальные"></a>
<a id="indexC:special"></a>
Но это ещё не конец всей истории.
`function` — это лишь костыль, на
который опиралась хромая реализация Лиспа.
С созданием первых компиляторов стало
ясно, что с точки зрения производительности у лексической области видимости есть
(ожидаемое при компиляции) преимущество: можно сгенерировать код для более-менее
прямого доступа к любой переменной, а не динамически отыскивать её значение
заново каждый раз.
Тогда по умолчанию стали делать все переменные лексическими,
за исключением тех, которые были явно помечены как динамические или, как тогда
их называли, _специальные_.
Выражение `(declare (special *x*))`
являлось командой компиляторам \LISP 1.5, \CommonLisp, Maclisp и других,
говорившей, что переменная *x* ведёт себя «особенно».

<a id="indexR:ссылочная прозрачность"></a>
Эффективность была не единственной причиной принятия такого решения.
Другой
причиной была потеря _ссылочной прозрачности_ (\english{referential
transparency}).
Ссылочная прозрачность — это свойство языка, заключающееся
в том, что замена в программе любого выражения его эквивалентом никак не изменит
поведение этой программы (оба варианта программы или вернут одно и то же
значение, или вместе застрянут в бесконечном цикле).
Например:

```scheme
(let ((x (lambda () 1))) (x)) |\eq| ((let ((x (lambda () 1))) x)) |\eq| 1
```

В общем случае ссылочная прозрачность теряется, если язык позволяет побочные
эффекты.
Чтобы она сохранилась и при наличии побочных эффектов, необходимо
точнее определить понятие эквивалентных выражений.
Scheme обладает ссылочной
прозрачностью, если не использовать присваивания, функции с побочными эффектами
и продолжения.
\seeEx[escape/ex:crazy-cc] Это свойство желаемо и в наших
программах, если мы хотим сделать их по-настоящему повторно используемыми, как
можно менее зависимыми от контекста использования.

<a id="indexR:переменные!безымянные"></a>
<a id="indexR:альфа@$\alpha$-конверсия"></a>
Локальные переменные функций вроде `(lambda (u) (+ u u))` иногда называются
*безымянными*.
Их имена ничего не значат и могут быть абсолютно
произвольными.
Функция `(lambda (n347) (+ n347 n347))` — это та же
самая\footnote{В терминах $\lambda$-исчисления подобная замена имён называется
$\alpha$-конверсией.} функция, что и `(lambda (u) (+ u u))`.

Мы ожидаем, что в языке будет сохраняться этот инвариант.
Но это невозможно
в динамическом Лиспе.
Рассмотрим следующий пример:

<a id="indexC:map"></a>
```scheme
(define (map fn l)  ; или `mapcar`, как кому нравится
  (if (pair? l)
      (cons (fn (car l)) (map fn (cdr l)))
      '() ) )

(let ((l '(a b c)))
  (map (lambda (x) (list-ref l x))
       '(2 1 0)))
```

(Функция `(list-ref $\ell$ *n*)` возвращает *n*‑й элемент
списка $\ell$.)

В Scheme мы бы получили `(c b a)`, но в динамическом Лиспе результатом будет
`(0 0 0)`! Причина: свободная переменная `l` в функции \ic{(lambda (x)
(list-ref l x))}, имя которой уже занято локальной переменной `l`
в `map`.

Это затруднение можно решить, просто изменив конфликтующие имена.
Например,
достаточно будет переименовать какую-нибудь из двух `l`.
Например, ту,
которая внутри `map`, потому что это более разумно.
Но какое имя выбрать,
чтобы эта проблема не возникла снова? Если приписывать спереди к имени каждой
переменной номер паспорта программиста, а сзади — текущее \UNIX-время, то
это, конечно, значительно снизит вероятность коллизий, но читабельность программ
будет оставлять желать лучшего.

В начале восьмидесятых годов сложилась довольно неприятная ситуация: студентов
учили Лиспу на примере интерпретаторов, но их понимание областей видимости
отличалось от понимания компиляторов.
В 1975 году Scheme [[ss75]](zz_bibliography.html#ss75) показал,
что интерпретатор и компилятор возможно примирить, поместив обоих в мир, где все
переменные лексические.
{\CommonLisp} забил последний гвоздь в гроб этой
проблемы, постановив, что *хорошее* понимание — это понимание
компилятора, а для него удобнее лексические переменные.
Интерпретатор должен
был подчиниться новым правилам.
Растущий успех Scheme и других функциональных
языков, вроде ML и компании, популяризовал новый подход сначала в языках
программирования, а затем и в умах людей.


\subsection{Дальнее и ближнее связывание}<!--  -->
\label{basics/representing-functions/ssect:deep-or-shallow}

<a id="indexR:дальнее (deep) связывание"></a>
<a id="indexR:связывание!дальнее (deep)"></a>
<a id="indexC:lookup!стоимость"></a>
Но не всё так просто заканчивается.
Разработчики языков нашли способы ускорить
поиск значений динамических переменных.
Если окружения представлены
ассоциативными списками, то время на поиск значения переменной (стоимость вызова
`lookup`) линейно зависит от длины списка.\footnote*{К счастью, статистика
показывает, что переменные, располагающиеся ближе к началу списка, используются
чаще тех, что находятся глубоко внутри.
Кстати, ещё стоит отметить, что
лексические окружения в среднем меньше по размеру, чем динамические, так как
последним необходимо хранить все переменные, участвующие в вычислениях, включая
одноимённые [[bak92a]](zz_bibliography.html#bak92a).} Такой подход называется _глубоким_ или
_дальним связыванием_ (deep binding), так как значения динамических
переменных обычно располагаются на некотором удалении от текущего локального
окружения.

<a id="indexR:ближнее (shallow) связывание"></a>
<a id="indexR:связывание!ближнее (shallow)"></a>
<a id="indexE:Cval"></a>
Существует и другой метод, называемый _поверхностным_ или \term{ближним
связыванием} (shallow binding).
Суть его в том, что переменная напрямую связана
с местом, где хранится её значение в текущий момент, без привязки к окружению.
Проще всего это реализовать, положив это значение в специальное поле символа,
соответствующего этой переменной; это поле называют `Cval` или \term{ячейкой
значения} (value cell).
В таком случае стоимость `lookup` постоянна или около
того: требуется лишь одна косвенная адресация и, может быть, сдвиг.
Так как
бесплатный сыр бывает только в мышеловке, то стоит отметить, что вызов функции
при использовании этого метода выходит дороже, потому что требуется сначала
где‑то сохранить старые значения аргументов, затем записать новые значения
в поля соответствующих символов.
А потом, что самое важное, после выхода из
функции старые значения в символах необходимо восстановить обратно, а это может
помешать оптимизации хвостовой рекурсии.
(Хотя есть варианты: [[sj93]](zz_bibliography.html#sj93).)

Изменив структуру окружений, мы сможем частично проэмулировать\footnote{Здесь мы
не реализуем присваивание переменным, захваченным замыканиями.
Об этом можно
почитать в [[bcsj86]](zz_bibliography.html#bcsj86).} ближнее связывание.
Но с оговорками: список
аргументов не может быть точечным (так будет легче его разбирать) и мы не будем
проверять арность функций.
Новые функции будем обозначать префиксом `s.`,
чтобы не путать их с другими.

<a id="indexC:s.make-function"></a>
<a id="indexC:s.lookup"></a>
<a id="indexC:s.update"!"></a>
```scheme
(define (s.make-function variables body env)
  (lambda (values current.env)
    (let ((old-bindings
           (map (lambda (var val)
                  (let ((old-value (getprop var 'apval)))
                    (putprop var 'apval val)
                    (cons var old-value) ) )
                variables
                values ) ))
      (let ((result (eprogn body current.env)))
        (for-each (lambda (b) (putprop (car b) 'apval (cdr b)))
                  old-bindings )
        result ) ) ) )

(define (s.lookup id env)
  (getprop id 'apval) )

(define (s.update! id env value)
  (putprop id 'apval value) )
```

<a id="indexC:putprop"></a><a id="indexC:getprop"></a>
В Scheme функции `putprop` и `getprop` не входят в стандарт, так как здесь
не любят неэффективные глобальные побочные эффекты, но тем не менее, даже
в [[as85]](zz_bibliography.html#as85) есть аналогичные `put` и `get`.
\seeEx[lisp1-2-omega/ex:write-put/get-prop]

<a id="indexR:списки свойств"></a>
<a id="indexR:символы!списки свойств"></a>
<a id="indexE:P-список"></a>
<a id="indexR:хеш-таблицы"></a>
С помощью этих функций мы эмулируем наличие у символов поля,\footnote*{Это поле
названо в честь `apval` из \cite{mae+62}.
\seePage[lisp1-2-omega/par:apval]
Тогда значения полей действительно хранились в наивных P‑списках.} где хранится
значение одноимённой переменной.
Независимо от их настоящей реализации,<!--  -->
\footnote*{Эти функции проходят по списку свойств символа (его P‑списку,
от property) до тех пор, пока не найдут нужное.
Скорость поиска, соответственно,
линейно зависит от длины списка, если только не применяются хеш-таблицы.} будем
считать, что они выполняются за постоянное время.

Заметьте, что в этой реализации абсолютно не используется окружение определения
`env`.
Поэтому для поддержки замыканий нам потребуется изменить реализацию
`make-closure`, так как она теперь не имеет доступа к окружению определения
(ввиду его отсутствия).
При создании замыкания необходимо просмотреть тело
функции, выделить все свободные переменные и правильно их сохранить внутри
замыкания.
Мы реализуем это позже.

<a id="indexE:rerooting"></a>
Дальнее связывание облегчает смену окружений и многозадачность, теряя в скорости
поиска переменных.
Ближнее связывание ускоряет поиск переменных, но теряет
в скорости вызова функций.
Генри Бейкеру [[bak78]](zz_bibliography.html#bak78) удалось объединить эти два
подхода в технику под названием _rerooting_.

Наконец, не забывайте, что ближнее и дальнее связывание — это лишь способы
реализации, они никак не влияют на само понятие связывания.


<a id="basics/sect:global-environment"></a>
## Глобальное окружение

<a id="indexR:библиотека!функций"></a>
<a id="indexR:Лисп!примитивы"></a>
Пустое глобальное окружение — это печально, поэтому большинство лисп-систем
предоставляют *библиотеки* функций.
Например, в глобальном окружении
{\CommonLisp} (CLtL1) около 700 функций, у {\LeLisp} их более 1500,
у {\ZetaLisp} — более 10\,000.
Без библиотек Лисп был бы лишь прикладным
$\lambda$-исчислением, в котором нельзя даже распечатать полученные результаты.
Библиотеки очень важны для конечного пользователя.
Специальные формы — это
строительные кирпичики для разработчиков интерпретаторов, но для конечного
пользователя такими кирпичиками являются функции библиотек.
По‑видимому, именно
отсутствие в чистом Лиспе таких банальных вещей вроде библиотеки
тригонометрических функций прочно укоренило мысль о непригодности Лиспа для
«серьёзных программ».
Как говорится в [[sla61]](zz_bibliography.html#sla61), возможность символьного
интегрирования или дифференцирования — это, конечно, замечательно, но кому
нужен язык, где нет даже синуса или тангенса?

Мы ожидаем, что все привычные функции вроде `cons`, `car` {\itp} будут
доступны в глобальном окружении.
Также можно туда поместить несколько простых
констант вроде логических значений и пустого списка.

Для этого мы определим пару макросов.
Исключительно для удобства, потому что мы
о них ещё даже не говорили.\footnote*{Согласитесь, было бы странным втискивать
всю книгу в первую главу.} Макросы — это довольно сложная и важная вещь сами
по себе, так что им посвящена собственная глава.
\seePage[chapter:macros]

Эти два макроса облегчат наполнение глобального окружения.
Само глобальное
окружение является расширением начального окружения `env.init`.

\ForLayout{display}{\begingroup
\lstset{aboveskip=\smallskipamount, belowskip=\smallskipamount}}

<a id="indexC:env.global"></a>
<a id="indexC:definitial"></a>
<a id="indexC:defprimitive"></a>
```scheme
(define env.global env.init)
|\ForLayout{display}{\vskip-0.4\baselineskip}|
(define-syntax definitial
  (syntax-rules ()
    ((definitial name)
     (begin (set! env.global (cons (cons 'name 'void) env.global))
            'name ) )
    ((definitial name value)
     (begin (set! env.global (cons (cons 'name value) env.global))
            'name ) ) ) )
|\ForLayout{display}{\vskip-0.4\baselineskip}|
(define-syntax defprimitive
  (syntax-rules ()
    ((defprimitive name value arity)
     (definitial name
        (lambda (values)
          (if (= arity (length values))
              (apply value values)      ; Родная `apply` Scheme
              (wrong "Incorrect arity" (list 'name values)|\:|)|\:|)|\:|)|\:|)|\:|)|\:|)|\:|)
```

<a id="indexC:the-false-value"></a>
Несмотря на то, что стандарт Scheme этого не требует, мы определим несколько
полезных констант.
Заметим, что `t` — это переменная в определяемом Лиспе,
а `\#t` — это значение из определяющего Лиспа.
Оно подходит, так как любое
значение, не совпадающее с `the-false-value`, является _истиной_.

```scheme
(definitial t #t)
(definitial f the-false-value)
(definitial nil '())
```

\ForLayout{display}{\endgroup}

<a id="indexR:синтаксис!для \protect`\#t` и `\#f`"></a>
<a id="indexR:логические значения"></a>
Хотя это удобно — иметь глобальные переменные с настоящими объектами для
данных сущностей, но есть и другое решение: особый синтаксис.
Scheme использует
`\#t` и `\#f`, подставляя вместо них логические _истину_ и
_ложь_.
В этом есть определённый смысл:

\begin{enumerate}
  \item Они всегда видимы: `\#t` означает _истину_ в любом
        контексте, даже тогда, когда локальная переменная
        названа `t`.

  \item Значение `\#t` невозможно изменить, но многие интерпретаторы
        позволят изменить значение глобальной переменной `t`.
\end{enumerate}

Например, выражение `(if t 1 2)` вернёт `2`, если оно вычисляется
в следующем окружении: `(let ((t \#f)) (if t 1 2))`.

<a id="indexC:eq?"></a>
Существует много способов ввести такой синтаксис.
Наиболее простой способ —
это вшить значения `t` и `f` в вычислитель:

```scheme
(define (evaluate e env)
  (if (atom? e)
      (cond ((eq? e 't) #t)
            ((eq? e 'f) #f)
            ...
            ((symbol? e) (lookup e env))
            ...
            (else (wrong "Cannot evaluate" exp)) )
      ...
) )
```

<a id="indexR:инлайнинг!функций"></a>
<a id="indexR:встраивание!функций"></a>
<a id="indexR:функции!встраиваемые"></a>
<a id="indexR:связывание!изменяемое"></a>
<a id="indexR:связывание!неизменяемое"></a>
Также мы могли бы ввести понятия _изменяемого_ и _неизменяемого_
связывания.
Неизменяемые переменные отвергаются присваиванием.
Ничто и никогда
не сможет изменить значение неизменяемой переменной.
Такая концепция существует,
хоть и не всегда явно, во многих системах.
Например, существуют так называемые
_инлайн-функции_ (также известные как _подставляемые_ или
_встраиваемые_), вызов которых можно полностью заменить прямой подстановкой
их тела.
\seePage[fast/fast/integrating/par:inlining]

Чтобы можно было спокойно подставить вместо `(car x)` код функции,
возвращающей левый элемент точечной пары `x`, необходимо быть абсолютно
уверенным в том, что значение глобальной переменной `car` никогда не менялось
и не поменяется в будущем.
Посмотрите, какая беда случается, если это не так:

```scheme
(set! my-global (cons 'c 'd))
   |\is| (c .
d)
(set! my-test (lambda () (car my-global)))
   |\is| #<MY-TEST procedure>
(begin (set! car cdr)
       (set! my-global (cons 'a 'b))
       (my-test) )
   |\is| |*?????*|
```

К счастью, в результате может получиться только `a` или `b`.
Если
`my-test` использует значение `car` на момент определения, то мы
получим `a`.
Если же `my-test` будет использовать текущее
значение `car`, то ответом будет `b`.
Полезным будет также сравнить в этом
аспекте `my-test` и `my-global`: обычно первый вариант поведения ожидается
от `my-test` при использовании компилятора, тогда как для `my-global`
нормальным считается именно второй вариант.
\seePage[lisp1-2-omega/recusion/simple/code:redefine]

<a id="indexC:foo"></a><a id="indexC:bar"></a><a id="indexC:fib"></a><a id="indexC:fact"></a>
Также мы добавим несколько рабочих переменных\footnote{К сожалению, сейчас они
ещё и инициализируются.
Эта ошибка будет исправлена позже.} в глобальное
окружение, так как сейчас у нас нет способа динамически создавать переменные.
По статистике, предлагаемые имена составляют приблизительно {96,037\,\<!-- } -->
используемых при тестировании свеженаписанных интерпретаторов.

<!-- \ForLayout{display}{\begingroup -->
<!-- \lstset{aboveskip=\smallskipamount, belowskip=\smallskipamount}} -->

```scheme
(definitial foo)
(definitial bar)
(definitial fib)
(definitial fact)
```

<!-- \ForLayout{display}{\endgroup} -->

Наконец, определим несколько примитивных функций (не все, потому что такие
полные списки — это хорошее снотворное).
Главная сложность состоит
в соединении механизмов вызова функций определяемого и определяющего языков.
Зная, что аргументы собираются нашим интерпретатором в список, достаточно просто
применить
к нему `apply`.\footnote*{Можно только порадоваться за наш выбор не называть
`invoke` «`apply`».} Заметьте, что арность функций будет соблюдаться,
так как мы включили проверку в определение макроса `defprimitive`.

```scheme
(defprimitive cons cons 2)
(defprimitive car car 1)
(defprimitive set-cdr! set-cdr! 2)
(defprimitive + + 2)
(defprimitive eq? eq? 2)
(defprimitive < < 2)
```


<a id="basics/sect:starting-the-interpreter"></a>
## Запускаем интерпретатор

Нам осталось показать только одну вещь: дверь в наш новый мир.

<a id="indexC:chapter1-scheme"></a>
```scheme
(define (chapter1-scheme)
  (define (toplevel)
    (display (evaluate (read) env.global))
    (toplevel) )
  (toplevel) )
```

Поскольку наш интерпретатор ещё мал и неопытен, но подаёт большие надежды,
предлагаем вам в качестве упражнения написать функцию, позволяющую из него
выйти.


<a id="basics/sect:conclusions"></a>
## Заключение

<a id="indexR:язык!и смысл программ"></a>
Действительно ли мы сейчас определили язык?

<a id="indexR:смысл программ"></a>
<a id="indexR:программы!смысл"></a>
Нет никаких сомнений в том, что мы можем запустить `evaluate`, передать ей
выражение, и она вскоре вернёт результат вычислений.
Но сама функция
`evaluate` не имеет никакого смысла без языка своего определения, а если
у нас нет определения языка определения, то мы вообще ни в чём не можем
быть уверены.
Так как каждый лиспер является дальним родственником барона
Мюнхгаузена, то, наверное, будет достаточно взять в качестве языка определения
тот, который мы только что определили.
Следовательно, у нас есть язык $L$,
определённый функцией `evaluate`, написанной на языке $L$.
Такой язык
является решением следующего уравнения относительно $L$:
<!--  -->
\begin{equation*}
  \forall\pi \in \Vset{Программы}\colon
    L\text{`(evaluate (quote $\pi$) env.global)`} \equiv L\pi
\end{equation*}

Исполнение любой программы $\pi$, написанной на $L$ (обозначается как $L\pi$),
должно вести себя так же (то есть давать тот же результат или никогда
не завершаться), как и выражение `(evaluate (quote $\pi$) env.global)` на
том же языке $L$.
Одним из занимательных следствий этого утверждения является
то, что `evaluate` способна\footnote{После того, как мы раскроем все
используемые макросы и сокращения вроде `let`, `case`, `define`
{\itd} Потом надо будет ещё поместить в глобальное окружение функции
`evaluate`, `evlis` и др.} проинтерпретировать сама себя.
Следовательно,
следующие выражения эквивалентны:

```scheme
(evaluate (quote |$\pi$|) env.global) |\eq|
  |\eq| (evaluate (quote (evaluate (quote |$\pi$|) env.global)) env.global)
```

Есть ли ещё решения приведённого уравнения? Да, и их великое множество! Как мы
видели раньше, определение `evaluate` вовсе не обязательно указывает порядок
вычислений.
Множество других свойств языка, используемого для определения,
бессознательно *наследуются* определяемым языком.
Мы, по сути, ничего
не можем о них сказать, но все эти варианты претендуют на решение указанного
уравнения.
Вместе с многочисленными тривиальными решениями.
Рассмотрим,
к примеру, язык $L_{2001}$, любая программа на котором возвращает $2001$.
Даже
такой язык удовлетворяет этому уравнению.
Поэтому для определения настоящих
языков необходимы другие методы, их мы рассмотрим в следующих главах.


<a id="basics/sect:exercises"></a>
## Упражнения

\begin{exercise}\label{basics/ex:tracer}
<a id="indexR:трассировка"></a>
Модифицируйте функцию `evaluate` так, чтобы она стала трассировщиком.
Все
вызовы функций должны выводить на экран фактические аргументы и возвращаемый
результат.
Легко представить себе дальнейшее развитие этого инструмента
в пошаговый отладчик, вдобавок позволяющий изменять порядок выполнения
отлаживаемой программы.
\end{exercise}

\begin{exercise}\label{basics/ex:excess-recursion}
Если функции `evlis` передаётся список из одного выражения, она делает
один лишний рекурсивный вызов.
Придумайте способ, как избавиться от него.
\end{exercise}

\begin{exercise}\label{basics/ex:new-extend}
Предположим, новая функция `extend` определена так:

<a id="indexC:extend"></a>
```scheme
(define (extend env names values)
  (cons (cons names values) env) )
```

Определите соответствующие функции `lookup` и `update!`.
Сравните их
с ранее рассмотренными.
\end{exercise}

\begin{exercise}\label{basics/ex:racks}
<a id="indexR:ближнее (shallow) связывание"></a>
<a id="indexR:связывание!ближнее (shallow)"></a>
<a id="indexE:rack"></a>
В работе [[ss80]](zz_bibliography.html#ss80) предлагается другой механизм ближнего связывания, названный
_rack_.
Символ связывается с полем, хранящим не единственное значение, а
стек значений.
В каждый момент времени значением переменной является находящаяся
на вершине стека величина.
Перепишите функции `s.make-function`,
`s.lookup` и `s.update!` для реализации этой идеи.
\end{exercise}

\begin{exercise}\label{basics/ex:liar-liar!}
<a id="indexR:представление!логических значений"></a>
Если вы ещё не заметили, то в определение функции `<` вкралась ошибка! Ведь
эта функция должна возвращать логические значения определяемого языка, а
не определяющего.
Исправьте это досадное недоразумение.
\end{exercise}

\begin{exercise}\label{basics/ex:def-list}
Определите функцию `list`.
\end{exercise}

\begin{exercise}\label{basics/ex:def-call/cc}
Для обожающих продолжения: определите `call/cc`.
\end{exercise}

\begin{exercise}\label{basics/ex:def-apply}
Определите функцию `apply`.
\end{exercise}

\begin{exercise}\label{basics/ex:def-end}
Определите функцию `end`, позволяющую выйти из интерпретатора, разработанного
в этой главе.
\end{exercise}

\begin{exercise}\label{basics/ex:slowpoke}
<a id="indexR:уровни интерпретации"></a>
<a id="indexR:интерпретация!уровневая"></a>
Сравните скорость Scheme и `evaluate`.
Затем сравните скорость `evaluate`
и `evaluate`, интерпретируемой с помощью `evaluate`.
\end{exercise}

\begin{exercise}\label{basics/ex:no-gensym}
Ранее мы смогли успешно определить `begin` через `lambda`
\seePage[basics/forms/sequence/par:gensym-puzzle], но для этого нам
потребовалось использовать функцию `gensym`, чтобы избежать коллизий имён
переменных.
Переопределите `begin` в таком же духе, но без использования
`gensym`.
\end{exercise}


\section*{Рекомендуемая литература}

Все работы по интерпретаторам, приведённые в начале этой главы, являются
довольно интересными, но если вы не можете столько читать, то вот наиболее
стоящие из них:
\begin{itemize}
  \item среди «$\lambda$‑papers»: [[ss78a]](zz_bibliography.html#ss78a);

  \item самая короткая в мире статья, которая содержит полный интерпретатор
        Лиспа: [[mcc78b]](zz_bibliography.html#mcc78b);

  \item «нестрого формальное» описание интерпретации: [[rey72]](zz_bibliography.html#rey72);

  \item местная книга Бытия: \cite{mae+62}.
\end{itemize}
